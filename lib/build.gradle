plugins {
    id 'java-library'
    id 'java-gradle-plugin'
}

group = 'abgnpr.plantumlgenerator'
version = '1.0.0'

gradlePlugin {
    plugins {
        plantUmlGenerator {
            id = 'abgnpr.plantuml-generator'
            implementationClass = 'plantumlgenerator.gradle.PlantUmlPlugin'
        }
    }
}

repositories {
    mavenCentral() // <2>
}

dependencies {
    implementation libs.javaparser.core
    compileOnly 'org.apache.maven:maven-plugin-api:3.9.6'
    compileOnly 'org.apache.maven.plugin-tools:maven-plugin-annotations:3.11.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.register('standaloneJar', Jar) {
    archiveClassifier = 'all'
    manifest {
        attributes 'Main-Class': 'plantumlgenerator.PlantUmlGenerator'
    }
    from sourceSets.main.output
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
